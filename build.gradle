plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    alias(libs.plugins.axionRelease)
    alias(libs.plugins.nexusPublish)
}

group = 'org.rundeck.hibernate'
ext.pluginName = 'Oracle Dialect'
ext.pluginDescription = 'Custom dialect for Oracle databases.'
ext.githubSlug = 'rundeck/rundeck-oracle-dialect'
ext.publishName = "Oracle Dialect' ${project.version}"
ext.developers = [
        [id: 'gschueler', name: 'Greg Schueler', email: 'greg@rundeck.com']
]


scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}

allprojects {
    project.version = scmVersion.version
}

defaultTasks 'clean','build'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.hibernate:hibernate-core:5.6.15.Final'
}

jar {
    manifest {
        attributes 'Implementation-Title': pluginName
        attributes 'Implementation-Description': pluginDescription
        attributes 'Implementation-Version': project.version
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

nexusPublishing {
    packageGroup = 'org.rundeck.hibernate'
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

apply from: "${rootDir}/gradle/publishing.gradle"