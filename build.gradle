plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

group = 'org.rundeck'
ext.pluginName = 'Oracle Dialect'
ext.pluginDescription = 'Custom dialect for Oracle databases.'
ext.githubSlug = 'rundeck/rundeck-oracle-dialect'
ext.developers = [
        [id: 'gschueler', name: 'Greg Schueler', email: 'greg@rundeck.com']
]

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}

allprojects {
    project.version = scmVersion.version
}

defaultTasks 'clean','build'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.hibernate:hibernate-core:5.6.15.Final'
}

jar {
    manifest {
        attributes 'Implementation-Title': pluginName
        attributes 'Implementation-Description': pluginDescription
        attributes 'Implementation-Version': project.version
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

nexusPublishing {
    packageGroup = 'org.rundeck'
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            
            pom {
                name = pluginName
                description = pluginDescription
                url = 'https://github.com/rundeck/rundeck-oracle-dialect'
                inceptionYear = '2018'
                
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                
                developers {
                    developer {
                        id = 'gschueler'
                        name = 'Greg Schueler'
                        email = 'greg@rundeck.com'
                    }
                }
                
                scm {
                    connection = 'scm:git:git://github.com/rundeck/rundeck-oracle-dialect.git'
                    developerConnection = 'scm:git:ssh://github.com:rundeck/rundeck-oracle-dialect.git'
                    url = 'https://github.com/rundeck/rundeck-oracle-dialect'
                }
            }
        }
    }
}